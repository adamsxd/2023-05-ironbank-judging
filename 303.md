ArmedGoose

medium

# No grace period after adjusting CollateralFactor/LiqThreshold may cause users to be liquidated immediately after

## Summary
The protocol has ability to adjust Collateral Factor and Liquidation Threshold for certain markets. Once adjusted, the new value may cause certain accounts to become liquidatable. There is no possibility for users to prevent that, unless they track changes in the protocol and overcollateralize in advance.

## Vulnerability Detail
Simply the functions [adjustMarketCollateralFactor](https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/MarketConfigurator.sol#L165) and [adjustMarketLiquidationThreshold](https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/MarketConfigurator.sol#L205) introduce any changes in real time, without checking if it will impact certain accounts.

A malicious MEV bot may be watching for such function calls in the mempool and calculate on-the-fly which accounts will fall into liquidation range after changes take effect, and place liquidation calls right after the adjustments transactions in order to liquidate users immediately. Similar lack of grace period was mentioned also [here](https://dacian.me/lending-borrowing-defi-attacks#heading-borrower-immediately-liquidated-after-repayments-resume).

## Impact
If protocol team decides to change collateral or liquidation threshold for a market, users will not be able to check in advance if they will become liquidatable and take any countermeasure.
The likelihood is low, since such adjustments are probably not going to happen often.
The impact is high, because some users will be unexpectedly liquidated.
Therefore, I rate the severity as medium.

## Code Snippet
Run following test: `forge test -vv --match-test test_liquidation_without_grace_period`
The test sets up a simple liquidation setup, and just shows an exemplary parameters adjustments which cause the users to become liquidatable in the same time. Exemplary values of collateralFactor an LiquidationThreshold are chosen very low for simplicity, but as you can imagine, in real life scenario there could be various collaterals of various values and a small change might cause some of them to fall into liquidatable level.

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "forge-std/Test.sol";
import "./Common.t.sol";

contract LiquidateTest is Test, Common {
    uint16 internal constant reserveFactor = 1000; // 10%
    int256 internal constant market1Price = 1500e8; //@audit-issue and this is why there is an overflow. high price + low decimals!
    int256 internal constant market2Price = 200e8;
    int256 internal constant market3Price = 300e8;
    uint16 internal constant market1CollateralFactor = 8000; // 80%
    uint16 internal constant market1LiquidationThreshold = 9000; // 90%
    uint16 internal constant market1LiquidationBonus = 11000; // 110%

    IronBank ib;
    MarketConfigurator configurator;
    CreditLimitManager creditLimitManager;
    TripleSlopeRateModel irm;
    FeedRegistry registry;
    PriceOracle oracle;

    ERC20Market market1;
    ERC20Market market2;
    ERC20Market market3;

    address admin = address(64);
    address user1 = address(128);
    address user2 = address(256);

    function setUp() public {
        ib = createIronBank(admin); //deploy the contract

        configurator = createMarketConfigurator(admin, ib); //create market configurator

        vm.prank(admin);
        ib.setMarketConfigurator(address(configurator)); //set market configurator addr to control the Iron Bank

        creditLimitManager = createCreditLimitManager(admin, ib); //create CreditLimitManager role

        vm.prank(admin);
        ib.setCreditLimitManager(address(creditLimitManager)); //set CreditLimitManager role

        irm = createDefaultIRM(); //create default interest rate model

        (market1,,) = createAndListERC20Market(18, admin, ib, configurator, irm, reserveFactor); //#decimals 18 -> 4 @audit-issue when 4 then overflow in borrow?
        (market2,,) = createAndListERC20Market(18, admin, ib, configurator, irm, reserveFactor); //create two the same markets just to have them ready to use
        (market3,,) = createAndListERC20Market(18, admin, ib, configurator, irm, reserveFactor); //create two the same markets just to have them ready to use

        registry = createRegistry();
        oracle = createPriceOracle(admin, address(registry)); //setup for using price oracles

        vm.prank(admin);
        ib.setPriceOracle(address(oracle)); //set the oracle

        setPriceForMarket(oracle, registry, admin, address(market1), address(market1), Denominations.USD, market1Price); 
        setPriceForMarket(oracle, registry, admin, address(market2), address(market2), Denominations.USD, market2Price);
        setPriceForMarket(oracle, registry, admin, address(market3), address(market3), Denominations.USD, market3Price); //set prices for the markets used in this test

        deal(address(market1), user1, 10000e18); //user 1 has decimals1 * market1 (10000e18)
        deal(address(market2), user2, 10000e18); //user 2 has decimals2 * market2
        deal(address(market3), user2, 10000e18);

        vm.startPrank(admin);
        configurator.configureMarketAsCollateral(
            address(market1), market1CollateralFactor, market1LiquidationThreshold, market1LiquidationBonus
        );//set  params for market1 

        // Inject some liquidity for borrow.
        market2.approve(address(ib), type(uint256).max);
        market3.approve(address(ib), type(uint256).max);
        ib.supply(admin, admin, address(market2), 10_000e18);
        ib.supply(admin, admin, address(market3), 10_000e18);
        vm.stopPrank();

        uint256 market1SupplyAmount = 100e18; //user 1 will supply this @todo also to test decimals decrease this #decimals 100e18 -> 4
        uint256 market2BorrowAmount = 200e18; //user 1 will borrow this
        uint256 market3BorrowAmount = 200e18;
        //@audit to test liquidation of a healty position, must have one more market for this user which will be overcollateralized.

        vm.startPrank(user1);
        market1.approve(address(ib), market1SupplyAmount);
        ib.supply(user1, user1, address(market1), market1SupplyAmount);
        ib.borrow(user1, user1, address(market2), market2BorrowAmount); //@audit-issue overflow when 4 decimals?
        ib.borrow(user1, user1, address(market3), market3BorrowAmount);
        vm.stopPrank();
    }


    function test_liquidation_without_grace_period() public {

        (uint256 collateralValue, uint256 debtValue) = ib.getAccountLiquidity(user1); 
        bool res1 = ib.isUserLiquidatable(user1);
        //removed the original assertions, they work anyway
        console.log("Is liquidatable user 1? ", res1);

        console.log("Now the collateral and liquidation threshold are immediately changed.");

        vm.prank(admin);
        configurator.adjustMarketCollateralFactor(address(market1), 1); //0 is not possible but let's say it is adjusted at some point
       
        vm.prank(admin);
        configurator.adjustMarketLiquidationThreshold(address(market1), 1); //0 is not possible but let's say it is adjusted at some point

        (collateralValue, debtValue) = ib.getAccountLiquidity(user1);

        bool result = ib.isUserLiquidatable(user1);
        console.log("Is liquidatable user 1? ", result);

        // User2 liquidates user1.
        uint256 repayAmount = 100e18;

        vm.startPrank(user2);
        market2.approve(address(ib), repayAmount);

        ib.liquidate(user2, user1, address(market2), address(market1), repayAmount);
        ib.redeem(user2, user2, address(market1), type(uint256).max);
        vm.stopPrank();

    }
}

```

![liq1](https://github.com/sherlock-audit/2023-05-ironbank-ArmedGoose/assets/120720673/87e59a89-ba97-438c-b4f7-0c779c32b227)


## Tool used
Foundry
Manual Review

## Recommendation
It would be better to implement such changes with a time delay, so users may already prepare themselves. New values for adjusted markets can be held in temporary parameters and assigned to the main parameters after a time delay. A public function similar to isUserLiquidatable, but counting the future (temporary) values instead of the main ones may be used, e.g. isUserLiquidatableAfterCurrentAdjustments, so users can call it and know if they are in danger and if so, supply new collateral in advance.
