plainshift-2

medium

# Certain functions may not return the latest account's liquidity state

## Summary

The following functions are vulnerable to the lack of accrual interest before calculations are made. That said, this report will focus on the `getAccountLiquidity()` function for simplicity.

1. IronBank.getAccountliquidity()
2. IronBank.isUserLiquidatable()
3. IronBank.deferLiquidityCheck()

`getAccountLiquidity()` retrieves a user's collateral and debt value. When retrieving this data, it's critical that interest has been accrued before the collateral and debt value is calculated. This ensures that the contract is data consistent with the current state of the contract. Unfortunately, at no time does the `getAccountLiquidity` accrue interest which means that any contract that calls this function will receive out-of-date data on the debt value, a critical component when calculating if a user has shortfall. 

## Vulnerability Detail

`_accrueInterest` is used in numerous operations to update the state of the contract. One of these states is the market's borrow index which is used to calculate a user's borrow balance:

```solidity
/**
 * @dev Get the borrow balance of a user.
 * @param m The storage of the market
 * @param user The address of the user
 * @return The borrow balance
 */
function _getBorrowBalance(DataTypes.Market storage m, address user) internal view returns (uint256) {
    DataTypes.UserBorrow memory b = m.userBorrows[user];

    if (b.borrowBalance == 0) {
        return 0;
    }

    // borrowBalanceWithInterests = borrowBalance * marketBorrowIndex / userBorrowIndex
    return (b.borrowBalance * m.borrowIndex) / b.borrowIndex;
}
```

If `_accrueInterest()` is never called, the `m.borrowIndex` in the `_getBorrowBalance()` function will contain an outdated borrow index. This can in turn result in `getAccountLiquidity()` returning a lower debt value since the borrow index will always increase over time.

Relevant `_getBorrowBalance` code can be found [here](https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/IronBank.sol#L840-L855).

## Impact

IronBank or third-party apps that call `getAccountLiquidity()` should expect to receive the user's current debt and collateral value. If the latest state is not received, then IronBank or a third-party app may make incorrect decisions.

## Code Snippet

```solidity

/**
 * @notice Get the account liquidity of a user.
 * @param user The address of the user
 * @return The account collateral value and borrow value
 */
function getAccountLiquidity(address user) public view returns (uint256, uint256) {
    // AUDIT: low-risk. _getAccountLiquidity utilizes _getBorrowBalance which checks the market borrow index and the borrower's last stored borrow index to determine the user's borrow balance. getAccountLiquidity returns an invalid liquidity value. Although this is a view function it should accrue interest before the user's liquidity is withdrawn.
    return _getAccountLiquidity(user);
}

/**
 * @dev Get the account liquidity of a user.
 * @param user The address of the user
 * @return The total collateral value and total debt value of the user
 */
function _getAccountLiquidity(address user) internal view returns (uint256, uint256) {
    uint256 collateralValue;
    uint256 debtValue;

    address[] memory userEnteredMarkets = allEnteredMarkets[user];
    for (uint256 i = 0; i < userEnteredMarkets.length; i++) {
        DataTypes.Market storage m = markets[userEnteredMarkets[I]];
        if (!m.config.isListed) {
            continue;
        }

        uint256 supplyBalance = m.userSupplies[user];
        uint256 borrowBalance = _getBorrowBalance(m, user);

        // AUDIT: i haven't finished reviewing this but this has the assumption of returning a 1e18 scaled price. confirm
        // this will always be the case.
        uint256 assetPrice = PriceOracleInterface(priceOracle).getPrice(userEnteredMarkets[I]);
        require(assetPrice > 0, "invalid price");
        uint256 collateralFactor = m.config.collateralFactor;
        if (supplyBalance > 0 && collateralFactor > 0) {
            uint256 exchangeRate = _getExchangeRate(m);
            collateralValue += (supplyBalance * exchangeRate * assetPrice * collateralFactor) / 1e36 / FACTOR_SCALE;
        }
        if (borrowBalance > 0) {
            debtValue += (borrowBalance * assetPrice) / 1e18;
        }
    }
    return (collateralValue, debtValue);
}

/**
 * @dev Get the borrow balance of a user.
 * @param m The storage of the market
 * @param user The address of the user
 * @return The borrow balance
 */
function _getBorrowBalance(DataTypes.Market storage m, address user) internal view returns (uint256) {
    DataTypes.UserBorrow memory b = m.userBorrows[user];

    if (b.borrowBalance == 0) {
        return 0;
    }

    // borrowBalanceWithInterests = borrowBalance * marketBorrowIndex / userBorrowIndex
    return (b.borrowBalance * m.borrowIndex) / b.borrowIndex;
}
```

## Tool used

Manual Review

## Recommendation

IronBank should add a check in `getAccountLiquidity()` to see if the interest has been accrued for the current block. If not, the function should revert. This will allow the function to maintain its view function state.
