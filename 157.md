deadrxsezzz

medium

# Adversary can gas grief innocent users for very little cost.

## Summary
Adversary can force innocent users to enter markets, making all further operations cost much more gas

## Vulnerability Detail
The current implementation of IronBank allows for adversary to supply assets for any user they want.
```solidity
function supply(address from, address to, address market, uint256 amount)
        external
        nonReentrant
        isAuthorized(from)
    {
        DataTypes.Market storage m = markets[market];
        require(m.config.isListed, "not listed");
        require(!m.config.isSupplyPaused(), "supply paused");
        require(!isCreditAccount(to), "cannot supply to credit account");

        _accrueInterest(market, m);

        if (m.config.supplyCap != 0) {
            uint256 totalSupplyUnderlying = m.totalSupply * _getExchangeRate(m) / 1e18;
            require(totalSupplyUnderlying + amount <= m.config.supplyCap, "supply cap reached");
        }

        uint256 ibTokenAmount = (amount * 1e18) / _getExchangeRate(m);

        // Update storage.
        m.totalCash += amount;
        m.totalSupply += ibTokenAmount;
        unchecked {
            // Overflow not possible: supplyBalance + ibTokenAmount is at most totalSupply + ibTokenAmount, which is checked above.
            m.userSupplies[to] += ibTokenAmount;
        }

        // Enter the market.
        if (amount > 0) {
            _enterMarket(market, to);
        }

        IBTokenInterface(m.config.ibTokenAddress).mint(to, ibTokenAmount); // Only emits Transfer event.
        IERC20(market).safeTransferFrom(from, address(this), amount);

        emit Supply(market, from, to, amount, ibTokenAmount);
    }
```
The problem with this is that an adversary can force a user to enter a market for as little as 1 wei, both by calling `supply()` or `transferIBTokens()`, making all further operations much more costly.
All of `borrow()`, `redeem()`, `transferIBTokens()` and `liquidate()` make a call to `_isLiquidatable()`. 
```solidity
address[] memory userEnteredMarkets = allEnteredMarkets[user];
        for (uint256 i = 0; i < userEnteredMarkets.length; i++) {
            DataTypes.Market storage m = markets[userEnteredMarkets[i]];
            if (!m.config.isListed) {
                continue;
            }

            uint256 supplyBalance = m.userSupplies[user];
            uint256 borrowBalance = _getBorrowBalance(m, user);

            uint256 assetPrice = PriceOracleInterface(priceOracle).getPrice(userEnteredMarkets[i]);
            require(assetPrice > 0, "invalid price");
            uint256 liquidationThreshold = m.config.liquidationThreshold;
            if (supplyBalance > 0 && liquidationThreshold > 0) {
                uint256 exchangeRate = _getExchangeRate(m);
                liquidationCollateralValue +=
                    (supplyBalance * exchangeRate * assetPrice * liquidationThreshold) / 1e36 / FACTOR_SCALE;
            }
            if (borrowBalance > 0) {
                debtValue += (borrowBalance * assetPrice) / 1e18;
            }
        }
```
Having been forced to enter the market, now `_isLiquidatable()` has to iterate over said market, going through the storage slot for the `Market` struct. Furthermore, it has to also make an external call to the Chainlink Oracle. All of these actions cost significant amounts of gas. 
To make things even worse, if the user decides to first redeem the assets used by the griefer and exit the market, the attacker can just repeat the attack for very little cost.

## Impact
Adversary can gas grief innocent users for close to no cost. ALL future operations will cost significantly more gas.

## Code Snippet
https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/IronBank.sol#L1065-#L1092
https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/IronBank.sol#L306-#L342


## Tool used

Manual Review

## Recommendation
Add access control to who can supply for a user or set a min Amount a user needs to send in `supply()` in order to make the attack much more costly.