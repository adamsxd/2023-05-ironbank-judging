vagrant

medium

# No price staleness check in PriceOracle

## Summary

Not checking the updatedAt value returned by latestRoundData can lead to stale prices being used for calculations which impact liquidations and other functions of the protocol

## Vulnerability Detail

latestRoundData() returns not just price but also other values such as the updatedAt value which can be used to determine whether the returned price is stale or not:

```solidity
    function getPriceFromChainlink(address base, address quote) internal view returns (uint256) {
        (, int256 price,,,) = registry.latestRoundData(base, quote);
        require(price > 0, "invalid price");

        // Extend the decimals to 1e18.
        return uint256(price) * 10 ** (18 - uint256(registry.decimals(base, quote)));
    }
```

The stale price can lead to incorrect calculations everytime PriceOracle#getPrice() is called:


```solidity
    function getPrice(address asset) external view returns (uint256) {
        if (asset == wsteth) {
            uint256 stEthPrice = getPriceFromChainlink(steth, Denominations.USD);
            uint256 stEthPerToken = WstEthInterface(wsteth).stEthPerToken();
            uint256 wstEthPrice = (stEthPrice * stEthPerToken) / 1e18;
            return getNormalizedPrice(wstEthPrice, asset);
        }

        AggregatorInfo memory aggregatorInfo = aggregators[asset];
        uint256 price = getPriceFromChainlink(aggregatorInfo.base, aggregatorInfo.quote);
        if (aggregatorInfo.quote == Denominations.ETH) {
            // Convert the price to USD based if it's ETH based.
            uint256 ethUsdPrice = getPriceFromChainlink(Denominations.ETH, Denominations.USD);
            price = (price * ethUsdPrice) / 1e18;
        }
        return getNormalizedPrice(price, asset);
    }
```

## Impact

Loss of user funds due to incorrect assumptions about the price, incorrect evaluation of amount seized by liquidation:
IronBank#_getLiquidationSeizeAmount() :

```solidity
    function _getLiquidationSeizeAmount(
        address marketBorrow,
        address marketCollateral,
        DataTypes.Market storage mCollateral,
        uint256 repayAmount
    ) internal view returns (uint256) {
        uint256 borrowMarketPrice = PriceOracleInterface(priceOracle).getPrice(marketBorrow);
        uint256 collateralMarketPrice = PriceOracleInterface(priceOracle).getPrice(marketCollateral);
        require(borrowMarketPrice > 0 && collateralMarketPrice > 0, "invalid price");

        // collateral amount = repayAmount * liquidationBonus * borrowMarketPrice / collateralMarketPrice
        // IBToken amount = collateral amount / exchangeRate
        //   = repayAmount * (liquidationBonus * borrowMarketPrice) / (collateralMarketPrice * exchangeRate)
        uint256 numerator = (mCollateral.config.liquidationBonus * borrowMarketPrice) / FACTOR_SCALE;
        uint256 denominator = (_getExchangeRate(mCollateral) * collateralMarketPrice) / 1e18;

        return (repayAmount * numerator) / denominator;
    }
```
but getPrice is also used in IronBank#_getAccountLiquidity() and _isLiquidatable() which can lead to liquidations that are not intended to happen:
https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/IronBank.sol#L1046
https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/IronBank.sol#L1079



## Code Snippet

https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/oracle/PriceOracle.sol#L66-L72

## Tool used

Manual Review

## Recommendation
implement a check that makes sure that the returned price from latestRoundData is not stale.
