bin2chen

medium

# maxFlashLoan() when isBorrowPaused() should return 0

## Summary
`FlashLoan.sol` funds the flash loan through the borrowing mechanism.
But `maxFlashLoan()` does not determine whether the borrow is suspended or not, and returns the wrong value

## Vulnerability Detail
`maxFlashLoan()` only check `isMarketListed`,  don't check isBorrowPaused()
```solidity
    function maxFlashLoan(address token) external view override returns (uint256) {
@>      if (!IronBankInterface(ironBank).isMarketListed(token)) {
            return 0;
        }

        DataTypes.MarketConfig memory config = IronBankInterface(ironBank).getMarketConfiguration(token);
        uint256 totalCash = IronBankInterface(ironBank).getTotalCash(token);
        uint256 totalBorrow = IronBankInterface(ironBank).getTotalBorrow(token);

        uint256 maxBorrowAmount;
        if (config.borrowCap == 0) {
            maxBorrowAmount = totalCash;
        } else if (config.borrowCap > totalBorrow) {
            uint256 gap = config.borrowCap - totalBorrow;
            maxBorrowAmount = gap < totalCash ? gap : totalCash;
        }

        return maxBorrowAmount;
    }
```

if `isBorrowPaused()` flashload can't borrow anything

```solidity
    function borrow(address from, address to, address market, uint256 amount)
        external
        nonReentrant
        isAuthorized(from)
    {
        DataTypes.Market storage m = markets[market];
        require(m.config.isListed, "not listed");
@>      require(!m.config.isBorrowPaused(), "borrow paused");
        require(m.totalCash >= amount, "insufficient cash");
```

## Impact

Non-compliance with EIP standards, leading to incorrect selection of third-party protocols

## Code Snippet
https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/flashLoan/FlashLoan.sol#L28

## Tool used

Manual Review

## Recommendation
add check `isBorrowPaused()`

```solidity
    function maxFlashLoan(address token) external view override returns (uint256) {
-       if (!IronBankInterface(ironBank).isMarketListed(token)) {
+       if (!IronBankInterface(ironBank).isMarketListed(token) || IronBankInterface(ironBank).getMarketConfiguration(token).isBorrowPaused()) {
            return 0;
        }
```
