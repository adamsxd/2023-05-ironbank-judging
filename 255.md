Bauchibred

medium

# Users Might Face Immediate Liquidation Post Loan Acquisition


## Summary

The `MarketConfigurator` contract in the protocol which seems to follow the concept of Compound V2's CF/LT, includes a function `configureMarketAsCollateral()`, which sets parameters related to collateral configuration. But the case is that the `collateralFactor` and `liquidationThreshold` parameters can be set to equal values. And when this happens, incentive for users to be immediately liquidated upon borrowing now exists, which seems impractical and potentially detrimental for a lending protocol.

## Vulnerability Detail

In the `configureMarketAsCollateral()` function, the `liquidationThreshold` is validated such that it should be greater than or equal to the `collateralFactor` (see the marked "@audit" comment). If both are set to equal values, it allows for immediate liquidation upon borrowing. This is because the collateral factor represents the proportion of borrowing power to the total collateral value, and the liquidation threshold denotes when a user's account can be liquidated. Thus, when they are equal, the user's account is liquidation eligible right after they borrow any amount.

This behavior seems questionable in the context of a lending platform. This means that as soon as a user borrows, they can be liquidated, which is not a healthy or practical operation for a lending protocol.

Take a look at [MarketConfigurator.sol#L114-L158](https://github.com/sherlock-audit/2023-05-ironbank/blob/9ebf1702b2163b55479624794ab7999392367d2a/ib-v2/src/protocol/pool/MarketConfigurator.sol#L114-L158), see the `@audit` tag for info.

```solidity
    /**
     * @notice Configure a market as collateral.
     * @dev This function is used for the first time to configure a market as collateral.
     * @param market The market to be configured
     * @param collateralFactor The collateral factor of the market
     * @param liquidationThreshold The liquidation threshold of the market
     * @param liquidationBonus The liquidation bonus of the market
     */
    function configureMarketAsCollateral(
        address market,
        uint16 collateralFactor,
        uint16 liquidationThreshold,
        uint16 liquidationBonus
    ) external onlyOwner {
        DataTypes.MarketConfig memory config = getMarketConfiguration(market);
        require(config.isListed, "not listed");
        require(
            config.collateralFactor == 0 && config.liquidationThreshold == 0 && config.liquidationBonus == 0,
            "already configured"
        );
        require(collateralFactor > 0 && collateralFactor <= MAX_COLLATERAL_FACTOR, "invalid collateral factor");
        require(
            liquidationThreshold > 0 && liquidationThreshold <= MAX_LIQUIDATION_THRESHOLD
                && liquidationThreshold >= collateralFactor,
            "invalid liquidation threshold"
        );
       //  @audit the above suggests that `liquidationThreshold = collateralFactor` is allowed which doesn't make sense as users can immediately get liquidated once they take out a loan which is not practical behaiviour
        require(
            liquidationBonus > MIN_LIQUIDATION_BONUS && liquidationBonus <= MAX_LIQUIDATION_BONUS,
            "invalid liquidation bonus"
        );
        require(
            uint256(liquidationThreshold) * uint256(liquidationBonus) / FACTOR_SCALE
                <= MAX_LIQUIDATION_THRESHOLD_X_BONUS,
            "liquidation threshold * liquidation bonus larger than 100%"
        );

        config.collateralFactor = collateralFactor;
        config.liquidationThreshold = liquidationThreshold;
        config.liquidationBonus = liquidationBonus;
        ironBank.setMarketConfiguration(market, config);

        emit MarketCollateralFactorSet(market, collateralFactor);
        emit MarketLiquidationThresholdSet(market, liquidationThreshold);
        emit MarketLiquidationBonusSet(market, liquidationBonus);
    }
```

## Impact

This can result in discouraging users from participating in the protocol as it leaves no room for price fluctuation and could potentially liquidate user positions immediately after a borrow operation. This might affect the overall health and adoption of the lending platform.

## Code Snippet

[MarketConfigurator.sol#L114-L158](https://github.com/sherlock-audit/2023-05-ironbank/blob/9ebf1702b2163b55479624794ab7999392367d2a/ib-v2/src/protocol/pool/MarketConfigurator.sol#L114-L158)

## Tool used

Manual Review

## Recommendation

It would be advisable to consider modifying the function to disallow setting the `liquidationThreshold` to be equal to the `collateralFactor`. The function could be revised to ensure that the `liquidationThreshold` is strictly greater than the `collateralFactor`, which would prevent the potential issue of immediate liquidation upon borrowing. The adjustment would be a minor change in the `require` statement that checks these parameters:

```solidity
require(
    liquidationThreshold > 0 && liquidationThreshold <= MAX_LIQUIDATION_THRESHOLD
        && liquidationThreshold > collateralFactor,
    "invalid liquidation threshold"
);
```

In this way, a user's account becomes eligible for liquidation only when the borrowed amount exceeds the borrowing power determined by the `collateralFactor`. It would be a healthier and more practical approach for a lending protocol, and it would give users some leeway to manage their positions in response to price fluctuations.
