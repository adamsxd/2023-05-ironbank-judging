bin2chen

medium

# setCreditLimit() CreditAccount may can't be canceled

## Summary
`setCreditLimit()` requires market `isListed` to be executed
So that when the market for `CreditAccount` is `delist`
then the user will never be able to cancel `CreditAccount`.

## Vulnerability Detail
Whether a user is a `CreditAccount` is determined by the length of the `allCreditMarkets[]` array, and there is no determination of whether the markets in the array are still `isListed`

```solidity
    function isCreditAccount(address user) public view returns (bool) {
@>      return allCreditMarkets[user].length > 0;
    }
```
Increases and decreases the length of the array when `CreditAccount` be set or cancel
```solidity
    function setCreditLimit(address user, address market, uint256 credit) external onlyCreditLimitManager {
        DataTypes.Market storage m = markets[market];
@>      require(m.config.isListed, "not listed");

        if (credit == 0 && creditLimits[user][market] != 0) {
@>          allCreditMarkets[user].deleteElement(market);
        } else if (credit != 0 && creditLimits[user][market] == 0) {
@>          allCreditMarkets[user].push(market);
        }

        creditLimits[user][market] = credit;
        emit CreditLimitChanged(user, market, credit);
    }
```
As in the code above, when add and cancel `CreditAccount` both determine if the marketplace is valid 
`require(m.config.isListed, "not listed")`

There is a problem, if the market is `delist`, the user cannot be canceled `CreditAccount`.
For example
1. suppose market[A] `isListed`
2. Alice is set to `CreditAccount` by `setCreditLimit(Alice, market[a],100)`
3. for some reason the market is forced to be `delist` by `hardDelistMarket()`
4. the administrator wants to turn off Alice's `CreditAccount` by `setCreditLimit(Alice, market[a],0)`,  it will revert and can never be turned off

Suggestion, only check the validity of the market when adding `CreditAccount`.


## Impact
Never be able to cancel a user's `CreditAccount`

## Code Snippet
https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/IronBank.sol#L636

https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/IronBank.sol#L240
## Tool used

Manual Review

## Recommendation

Check the validity of the market only when adding `CreditAccount`.

```solidity
    function setCreditLimit(address user, address market, uint256 credit) external onlyCreditLimitManager {
        DataTypes.Market storage m = markets[market];
-       require(m.config.isListed, "not listed");

        if (credit == 0 && creditLimits[user][market] != 0) {
            allCreditMarkets[user].deleteElement(market);
        } else if (credit != 0 && creditLimits[user][market] == 0) {
+           require(m.config.isListed, "not listed");
            allCreditMarkets[user].push(market);
        }

        creditLimits[user][market] = credit;
        emit CreditLimitChanged(user, market, credit);
    }
```
