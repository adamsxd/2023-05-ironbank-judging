saidam017

medium

# delist market not clearing user borrows and user supplies mapping, could cause issue if market listed again

## Summary

Market configurator can `hardDelistMarket` market and eventually will trigger Iron Bank's `delistMarket` to delete the market from storage and from all markets array. However, it doesn't delete the mapping of `userBorrows` and `userSupplies` inside `markets` mapping. This can cause issue if market listed again.

## Vulnerability Detail

Consider this scenario : 

1. Before market A delisted, Alice still has 1000 Token A `userBorrows` borrow in market.
2. Now the market configurator trigger `hardDelistMarket` :

https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/MarketConfigurator.sol#L305-L330

```solidity
    function hardDelistMarket(address market) external onlyOwner {
        DataTypes.MarketConfig memory config = getMarketConfiguration(market);
        require(config.isListed, "not listed");
        require(config.isSupplyPaused() && config.isBorrowPaused(), "not paused");
        require(config.reserveFactor == MAX_RESERVE_FACTOR, "reserve factor not max");
        require(
            config.collateralFactor == 0 && config.liquidationThreshold == 0,
            "collateral factor or liquidation threshold not zero"
        );

        if (config.isPToken) {
            address underlying = PTokenInterface(market).getUnderlying();
            DataTypes.MarketConfig memory underlyingConfig = getMarketConfiguration(underlying);
            // It's possible that the underlying is not listed.
            if (underlyingConfig.isListed && underlyingConfig.pTokenAddress != address(0)) {
                underlyingConfig.pTokenAddress = address(0);
                ironBank.setMarketConfiguration(underlying, underlyingConfig);

                emit MarketPTokenSet(underlying, address(0));
            }
        }

        ironBank.delistMarket(market);

        emit MarketDelisted(market);
    }
```

and eventually trigger `ironBank.delistMarket(market)` : 

https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/IronBank.sol#L599-L607

```solidity
    /**
     * @notice Delist a market.
     * @dev This function is callable by the market configurator only.
     * @param market The address of the market
     */
    function delistMarket(address market) external onlyMarketConfigurator {
        DataTypes.Market storage m = markets[market];
        require(m.config.isListed, "not listed");

        delete markets[market];
        allMarkets.deleteElement(market);

        emit MarketDelisted(market);
    }
```

notice that inside `delistMarket`, the function delete `markets` mapping storage, inside this storage it hold this `Market` struct : 

```solidity
          struct Market {
            MarketConfig config;
            uint40 lastUpdateTimestamp;
            uint256 totalCash;
            uint256 totalBorrow;
            uint256 totalSupply;
            uint256 totalReserves;
            uint256 borrowIndex;
            mapping(address => UserBorrow) userBorrows;
            mapping(address => uint256) userSupplies;
        }
```

this will delete all (totalCash, totalBorrow etc.) but `userBorrows` and `userSupplies` are not deleted, so the unpaid users borrow and users supply still stuck hold inside this mapping. This happened because If you have a mapping in a struct or in your state in general, Solidity cannot delete it, because it does not know the keys for the mapping.

4. In the future, market is listed again by market configurator 

5. Alice trigger `repay` for this re listed market, (recall that she has 1000 debt token A that still inside  `userBorrows`, this call will cause `m.totalBorrow` to underflow : 

https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/IronBank.sol#L460-L470

```solidity
    function repay(address from, address to, address market, uint256 amount) external nonReentrant isAuthorized(from) {
        DataTypes.Market storage m = markets[market];
        require(m.config.isListed, "not listed");
        if (isCreditAccount(to)) {
            require(from == to, "credit account can only repay for itself");
        }

        _accrueInterest(market, m);

        _repay(m, from, to, market, amount);
    }
```

inside repay, `m.totalBorrow` will be decrement by value of Alice  `userBorrows`, if market just listed again, this will cause underflow : 

```solidity
    function _repay(DataTypes.Market storage m, address from, address to, address market, uint256 amount)
        internal
        returns (uint256)
    {
        uint256 borrowBalance = _getBorrowBalance(m, to);
        if (amount == type(uint256).max) {
            amount = borrowBalance;
        }

        require(amount <= borrowBalance, "repay too much"); // @audit repay check is only against the user borrow (which not deleted when delist market)

        uint256 newUserBorrowBalance;
        uint256 newTotalBorrow;
        unchecked {
            newUserBorrowBalance = borrowBalance - amount;
            // Underflow not possible: amount <= userBorrow <= totalBorrow
            newTotalBorrow = m.totalBorrow - amount; //@audit - this will underflow
        }

        // Update storage.
        m.userBorrows[to].borrowBalance = newUserBorrowBalance;
        m.userBorrows[to].borrowIndex = m.borrowIndex;
        m.totalCash += amount;
        m.totalBorrow = newTotalBorrow;

        // Check if need to exit the market.
        if (m.userSupplies[to] == 0 && newUserBorrowBalance == 0) {
            _exitMarket(market, to);
        }

        IERC20(market).safeTransferFrom(from, address(this), amount);

        emit Repay(market, from, to, amount, newUserBorrowBalance, newTotalBorrow);

        return amount;
    }
```

## Impact

This can cause the value of market `totalBorrow` is very high (caused by underflow), and make the following market not working properly).

## Code Snippet

https://github.com/sherlock-audit/2023-05-ironbank/blob/main/ib-v2/src/protocol/pool/IronBank.sol#L599-L607

## Tool used

Manual Review

## Recommendation

Consider to remove unchecked operation inside `_repay` function.
